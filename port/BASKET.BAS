1 RANDOMIZE \ DIM S(1)
2 PRINT "THIS IS DARTMOUTH COLLEGE BASKETBALL.  YOU WILL BE DARTMOUTH"
3 PRINT "  CAPTAIN AND PLAYMAKER.  CALL SHOTS AS FOLLOWS:  1. LONG"
4 PRINT "  (30 FT.) JUMP SHOT; 2  SHORT (15 FT.) JUMP SHOT; 3. LAY"
5 PRINT "  UP; 4. SET SHOT."
6 PRINT "BOTH TEAMS WILL USE THE SAME DEFENSE. CALL DEFENSE AS"
7PRINT"  FOLLOWS: 6  PRESS;  6.5  MAN-TO-MAN;  7  ZONE;  7.5  NONE."
8 PRINT "  TO CHANGE DEFENSE, JUST TYPE   0   AS YOUR NEXT SHOT."
9 PRINT "YOUR STARTING DEFENSE WILL BE";
10 INPUT D
11 IF D<6 THEN 126
12 PRINT
13 PRINT "CHOOSE YOUR OPPONENT";
14 INPUT O$
15 PRINT "CENTER JUMP"
16 IF RND(0) > 3/5 THEN 19
17 PRINT O$;" CONTROLS THE TAP."
18 GO TO 130
19 PRINT "DARTMOUTH CONTROLS THE TAP."
20 PRINT
21 PRINT "YOUR SHOT";
22 LET P=0
23 INPUT Z
24 IF Z<>INT(Z) THEN 27
25IF ABS(Z-2)>2 THEN 27
26 GOTO 29
27 PRINT "INCORRECT ANSWER.  RETYPE IT";
28 GOTO 23
29 GO TO 30
30 IF RND(0) <.5 THEN 48
31 IF T<100 THEN 48
32 PRINT
33 IF S(1)<>S(0) THEN 41
34 PRINT "   ***** END OF SECOND HALF *****"
35 PRINT "SCORE AT END OF REGULATION TIME:"
36 PRINT "        DARTMOUTH" S(1); O$" "S(0)
37 PRINT
38 PRINT "BEGIN TWO MINUTE OVERTIME PERIOD"
39 LET T=93
40 GO TO 15
41 PRINT "   ***** END OF GAME *****"
42 PRINT "FINAL SCORE: DARTMOUTH" S(1); O$" "S(0)
43 STOP
44 PRINT
45 PRINT "  ***  TWO MINUTES LEFT IN THE GAME  ***"
46 PRINT
47 RETURN
48 IF Z=1 THEN 51
49 IF Z=2 THEN 51
50 GO TO 92
51 LET T=T+1
52 IF T=50 THEN 199
53 IF T=92 THEN 55
54 GO TO 56
55 GOSUB 44
56 PRINT "JUMP SHOT"
57 IF RND(0) >.341*D/8 THEN 61
58 PRINT "SHOT IS GOOD."
59 GOSUB 196
60 GO TO 130
61 IF RND(0) >.682*D/8 THEN 79
62 PRINT "SHOT IS OFF TARGET."
63 IF D/6*RND(0)>.45 THEN 66
64 PRINT "DARTMOUTH CONTROLS THE REBOUND."
65 GO TO 68
66 PRINT "REBOUND TO ";O$;"."
67 GO TO 130
68 IF RND(0)>.40 THEN 70
69 GO TO 92
70 IF D=6 THEN 188
71 PRINT "BALL PASSED BACK TO YOU. ";
72 GO TO 21
73 IF RND(0)>.9THEN77
74 PRINT "PLAYER FOULED. TWO SHOTS."
75 GOSUB 171
76 GO TO 130
77 PRINT "BALL STOLEN. ";O$;"'S BALL."
78 GO TO 130
79 IF RND(0)>.782*D/8 THEN 86
80 PRINT "SHOT IS BLOCKED.  BALL CONTROLLED BY";
81 IF RND(0)>.5 THEN 84
82 PRINT " DARTMOUTH."
83 GO TO 21
84 PRINT " ";O$;"."
85 GO TO 130
86 IF RND(0)>.843*D/8 THEN 90
87 PRINT "SHOOTER IS FOULED.  TWO SHOTS."
88 GOSUB 171
89 GO TO 130
90 PRINT "CHARGING FOUL.  DARTMOUTH LOSES THE BALL."
91 GO TO 130
92 LET T=T+1
93 IF T=50 THEN 199
94 IF T=92 THEN 96
95 GO TO 97
96 GOSUB 44
97 IF Z=0 THEN 126
98 IF Z>3 THEN 124
99 PRINT "LAY UP."
100 IF 7/D*RND(0)>.4 THEN 104
101 PRINT "SHOT IS GOOD.  TWO POINTS."
102 GOSUB 196
103GO TO 130
104 IF 7/D*RND(0)>.7 THEN 115
105 PRINT "SHOT IS OFF THE RIM."
106 IF RND(0)>2/3 THEN 110
107 PRINT O$;" CONTROLS THE REBOUND."
108 GO TO 130
109
110 PRINT "DARTMOUTH CONTROLS THE REBOUND."
111 IF RND(0)>.4 THEN 113
112 GO TO 92
113 PRINT "BALL PASSED BACK TO YOU.";
114 GO TO 21
115 IF 7/D*RND(0)>.875 THEN 119
116 PRINT "SHOOTER FOULED.  TWO SHOTS."
117 GOSUB 171
118 GO TO 130
119 IF 7/D*RND(0)>.925 THEN 122
120 PRINT "SHOT BLOCKED. "O$;"'S BALL."
121 GO TO 130
122 PRINT "CHARGING FOUL.  DARTMOUTH LOSES THE BALL."
123 GO TO 130
124 PRINT "SET SHOT"
125 GO TO 100
126 PRINT "YOUR NEW DEFENSIVE ALLIGNMENT IS";
127 INPUT D
128 IF D<6 THEN 126
129 GO TO 20
130 LET P=1
131 LET T=T+1
132 IF T=54 THEN 199
133 IF T=92 THEN 135
134 GO TO 136
135 GOSUB 44
136 PRINT
137 LET Z1=10/4*RND(0)+1
138 IF Z1>2 THEN 161
139PRINT "JUMP SHOT"
140 IF 8/D*RND(0)>.35 THEN 144
141 PRINT "SHOT IS GOOD."
142 GOSUB 193
143 GO TO 20
144 IF 8/D*RND(0)>.75 THEN 155
145 PRINT "SHOT IS OFF THE RIM."
146 IF D/6*RND(0)>.5 THEN 149
147 PRINT "DARTMOUTH CONTROLS THE REBOUND."
148 GO TO 20
149 PRINT O$;" CONTROLS THE REBOUND."
150 IF D=6 THEN 183
151 IF RND(0)>.5 THEN 154
152 PRINT "PASS BACK TO " O$;" GUARD."
153 GO TO 130
154 GO TO 161
155 IF 8/D*RND(0)>.9 THEN 159
156 PRINT "PLAYER FOULED.  TWO SHOTS."
157 GOSUB 171
158 GO TO 20
159 PRINT "OFFENSIVE FOUL. DARTMOUTH'S BALL"
160 GO TO 20
161 IF Z1>3 THEN 169
162 PRINT "LAY UP"
163 IF 7/D*RND(0)>.413 THEN 167
164 PRINT "SHOT IS GOOD."
165 GOSUB 193
166 GO TO 20
167 PRINT "SHOT IS MISSED."
168 GO TO 146
169 PRINT "SET SHOT"
170 GO TO 163
171 REM *FOUL SHOOTING*
172 IF RND(0)>.49 THEN 177
173 PRINT "SHOOTER MAKES BOTH SHOTS."
174 LET S(1-P)=S(1-P)+2
175 GOSUB 194
176 RETURN
177 IF RND(0)>.75 THEN 181
178 PRINT "SHOOTER MAKES ONE SHOT AND MISSES ONE."
179 LET S(1-P)=S(1-P)+1
180 GO TO 175
181 PRINT "BOTH SHOTS MISSED."
182 GO TO 175
183 IF RND(0)>.75 THEN 185
184 GO TO 151
185 PRINT "BALL STOLEN.  EASY LAY-UP FOR DARTMOUTH."
186 GOSUB 196
187 GO TO 130
188 IF RND(0)>.6 THEN 190
189 GO TO 71
190 PRINT "PASS STOLEN BY ";O$;" EASY LAY-UP."
191 GOSUB 193
192 GO TO 20
193 LET S(0)=S(0)+2
194 PRINT "SCORE: " S(1); "TO " S(0)
195 RETURN
196 LET S(1)=S(1)+2
197 GOSUB 194
198 RETURN
199 PRINT "   ***** END OF FIRST HALF *****"
200 PRINT"SCORE: DARTMOUTH" S(1); O$" "S(0)
201 PRINT
202 PRINT
203 GO TO 15
204 END
