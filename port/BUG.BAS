   1 REM BRIAN MONTE LEIBOWITZ GRADE 7
   2 REM HARRISON JR. HIGH SCHOOL
   3 REM HARRISON N.Y.
   4 RANDOMIZE
   5 A=0\B=0\H=0\L=0\N=0\P=0\Q=0\R=0\S=0\T=0\U=0\V=0\Y=0
   6 PRINT "THE GAME BUG IS LIKE THE GAME COOTI,"
   7 PRINT "I HOPE YOU ENJOY THIS GAME"
   8 PRINT
   9 PRINT "DO YOU WANT INSTRUCTIONS";
  10 INPUT Z$
  11 IF Z$="NO" THEN 30
  12 PRINT "THE OBJECT OF BUG IS TO FINISH YOUR BUG BEFORE I FINISH MINE"
  13 PRINT "EACH NUMBER STANDS FOR A PART OF THE BUGS BODY"
  14 PRINT "I WILL ROLL THE DIE FOR YOU, TELL YOU WHAT I ROLLED FOR YOU"
  15 PRINT "WHAT THE NUMBER STANDS FOR, AND IF YOU CAN GET THE PART."
  16 PRINT "IF YOU CAN GET THE PART I WILL GIVE IT TO YOU."
  17 PRINT "THE SAME WILL HAPPEN ON MY TURN"
  18 PRINT "IF THERE IS A CHANGE IN EITHER BUG I WILL GIVE YOU THE "
  19 PRINT "OPTION OF SEEING THE PICTURES OF THE BUGS."
  20 PRINT "THE NUMBERS STAND FOR PARTS AS FOLLOWS:"
  21 PRINT "NUMBER","PART","NUMBER OF PART NEEDED"
  22 PRINT "1","BODY","1"
  23 PRINT "2","NECK","1"
  24 PRINT "3","HEAD","1"
  25 PRINT "4","FEELERS","2"
  26 PRINT "5","TAIL","1"
  27 PRINT "6","LEGS","6"
  28 PRINT
  29 PRINT
  30 IF Y>0 THEN 248
  31 Z=INT(6*RND(0)+1)
  32 C=1
  33 PRINT "YOU ROLLED A "Z
  34 ON Z GOTO 35,43,54,65,76,87
  35 PRINT "1=BODY"
  36 IF B=1 THEN 41
  37 PRINT "YOU NOW HAVE A BODY"
  38 B=1
  39 C=0
  40 GOTO 97
  41 PRINT "YOU DO NOT NEED A BODY"
  42 GOTO 97
  43 PRINT "2=NECK"
  44 IF N=1 THEN 50
  45 IF B=0 THEN 52
  46 PRINT "YOU NOW HAVE A NECK"
  47 N=1
  48 C=0
  49 GOTO 97
  50 PRINT "YOU DO NOT NEED A NECK"
  51 GOTO 97
  52 PRINT "YOU DO NOT HAVE A BODY"
  53 GOTO 97
  54 PRINT "3=HEAD"
  55 IF N=0 THEN 61
  56 IF H=1 THEN 63
  57 PRINT "YOU NEEDED A  HEAD"
  58 H=1
  59 C=0
  60 GOTO 97
  61 PRINT "YOU DO NOT HAVE A NECK"
  62 GOTO 97
  63 PRINT "YOU HAVE A HEAD"
  64 GOTO 97
  65 PRINT "4=FEELERS"
  66 IF H=0 THEN 74
  67 IF A=2 THEN 72
  68 PRINT "I NOW GIVE YOU A FEELER"
  69 LET A=A+1
  70 C=0
  71 GOTO 97
  72 PRINT "YOU HAVE TWO FEELERS ALREADY"
  73 GOTO 97
  74 PRINT "YOU DO NOT HAVE A HEAD"
  75 GOTO 97
  76 PRINT "5=TAIL"
  77 IF B=0 THEN 83
  78 IF T=1 THEN 85
  79 PRINT "I NOW GIVE YOU A TAIL"
  80 LET T=T+1
  81 C=0
  82 GOTO 97
  83 PRINT "YOU DO NOT HAVE A BODY"
  84 GOTO 97
  85 PRINT "YOU ALREADY HAVE A TAIL"
  86 GOTO 97
  87 PRINT "6=LEG"
  88 IF L=6 THEN 94
  89 IF B=0 THEN 96
  90 LET L=L+1
  91 C=0
  92 PRINT "YOU NOW HAVE "L" LEG(S)"
  93 GOTO 97
  94 PRINT "YOU HAVE 6 FEET ALREADY"
  95 GOTO 97
  96 PRINT "YOU DO NOT HAVE A BODY"
  97 LET X=INT(6*RND(0)+1)
  98 PRINT "I ROLLED A "X
  99 ON X GOTO 100,108,119,130,141,152
 100 PRINT "1=BODY"
 101 IF P=1 THEN 106
 102 PRINT "I NOW HAVE A BODY"
 103 C=0
 104 P=1
 105 GOTO 163
 106 PRINT "I DO NOT NEED A BODY"
 107 GOTO 163
 108 PRINT "2=NECK"
 109 IF Q=1 THEN 115
 110 IF P=0 THEN 117
 111 PRINT "I NOW HAVE A NECK"
 112 Q=1
 113 C=0
 114 GOTO 163
 115 PRINT "I DO NOT NEED A NECK"
 116 GOTO 163
 117 PRINT "I DO NOT  HAVE A BODY"
 118 GOTO 163
 119 PRINT "3=HEAD"
 120 IF Q=0 THEN 126
 121 IF R=1 THEN 128
 122 PRINT "I NEEDED A HEAD"
 123 R=1
 124 C=0
 125 GOTO 163
 126 PRINT "I DO NOT HAVE A NECK"
 127 GOTO 163
 128 PRINT "I DO NOT NEED A HEAD"
 129 GOTO 163
 130 PRINT "4=FEELERS"
 131 IF R=0 THEN 139
 132 IF S=2 THEN 137
 133 PRINT "I GET A FEELER"
 134 LET S=S+1
 135 C=0
 136 GOTO 163
 137 PRINT "I HAVE 2 FEELERS ALREADY"
 138 GOTO 163
 139 PRINT "I DO NOT HAVE A HEAD"
 140 GOTO 163
 141 PRINT "5=TAIL"
 142 IF P=0 THEN 148
 143 IF U=1 THEN 150
 144 PRINT "I NOW HAVE A TAIL"
 145 U=1
 146 C=0
 147 GOTO 163
 148 PRINT "I DO NOT HAVE A BODY"
 149 GOTO 163
 150 PRINT "I DO NOT NEED A TAIL"
 151 GOTO 163
 152 PRINT "6=LEGS"
 153 IF V=6 THEN 159
 154 IF P=0 THEN 161
 155 LET V=V+1
 156 C=0
 157 PRINT "I NOW HAVE "V" LEG(S)"
 158 GOTO 163
 159 PRINT "I HAVE 6 FEET"
 160 GOTO 163
 161 PRINT "I DO NOT HAVE A BODY"
 162 GOTO 163
 163 IF A<>2 THEN 167\IF T<>1 THEN 167\IF L=6 THEN 165
 164 GOTO 167
 165 PRINT "YOUR BUG IS FINISHED"
 166 LET Y=Y+1
 167 IF S<>2 THEN 171\IF P<>1 THEN 171\IF V=6 THEN 169
 168 GOTO 171
 169 PRINT "MY BUG IS FINISHED"
 170 LET Y=Y+2
 171 IF C=1 THEN 30
 172 PRINT "DO YOU WANT THE PICTURES";
 173 INPUT Z$
 174 IF Z$="NO" THEN 30
 175 PRINT "*****YOUR BUG*****"
 176 PRINT
 177 PRINT
 178 IF A=0 THEN 186
 179 FOR Z=1 TO 4
 180 PRINT TAB(10);
 181 FOR X=1 TO A
 182 PRINT "A ";
 183 NEXT X
 184 PRINT
 185 NEXT Z
 186 IF H=0 THEN 188
 187 GOSUB 247
 188 IF N=0 THEN 192
 189 FOR Z=1 TO 2
 190 PRINT "          N N"
 191 NEXT Z
 192 IF B=0 THEN 200
 193 PRINT "     BBBBBBBBBBBB"
 194 FOR Z=1 TO 2
 195 PRINT "     B          B"
 196 NEXT Z
 197 IF T<>1 THEN 199
 198 PRINT "TTTTTB          B"
 199 PRINT "     BBBBBBBBBBBB"
 200 IF L=0 THEN 208
 201 FOR Z=1 TO 2
 202 PRINT TAB(5);
 203 FOR X=1 TO L
 204 PRINT " L";
 205 NEXT X
 206 PRINT
 207 NEXT Z
 208 FOR Z=1 TO 4
 209 PRINT
 210 NEXT Z
 211 PRINT "*****MY BUG*****"
 212 PRINT
 213 PRINT
 214 PRINT
 215 IF S=0 THEN 223
 216 FOR Z=1 TO 4
 217 PRINT TAB(10);
 218 FOR X=1 TO S
 219 PRINT "F ";
 220 NEXT X
 221 PRINT
 222 NEXT Z
 223 IF R<>1 THEN 225
 224 GOSUB 247
 225 IF Q=0 THEN 228
 226 PRINT "          N N"
 227 PRINT "          N N"
 228 IF P=0 THEN 236
 229 PRINT "     BBBBBBBBBBBB"
 230 FOR Z=1 TO 2
 231 PRINT "     B          B"
 232 NEXT Z
 233 IF U<>1 THEN 235
 234 PRINT "TTTTTB          B"
 235 PRINT "     BBBBBBBBBBBB"
 236 IF V=0 THEN 245
 237 FOR Z=1 TO 2
 238 PRINT TAB(5);
 239 FOR X=1 TO V
 240 PRINT " L";
 241 NEXT X
 242 PRINT
 243 NEXT Z
 245 IF Y<>0 THEN 254
 246 GOTO 30
 247 PRINT "        HHHHHHH"
 248 PRINT "        H     H"
 249 PRINT "        H O O H"
 250 PRINT "        H     H"
 251 PRINT "        H  V  H"
 252 PRINT "        HHHHHHH"
 253 RETURN
 254 PRINT "I HOPE YOU ENJOYED THE GAME, PLAY IT AGAIN SOON !!!"
 255 END
